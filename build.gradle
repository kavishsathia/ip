plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'

    def javaFxVersion = '17.0.6'
    String[] javaFxModules = ['javafx-base', 'javafx-controls', 'javafx-graphics']
    String[] platforms = ['win', 'mac', 'linux']
    for (module in javaFxModules) {
        for (platform in platforms) {
            implementation "org.openjfx:${module}:${javaFxVersion}:${platform}"
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClass.set("patrick.gui.Launcher")
}

shadowJar {
    archiveFileName = 'patrick.jar'
    mergeServiceFiles()
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}

run{
    standardInput = System.in
}
